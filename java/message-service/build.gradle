plugins {
    id 'org.springframework.boot' version '3.0.0'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

group = 'io.honeycomb.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
    distroVersion = '0.10.0'
}

configurations {
    agent
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    agent "io.honeycomb:honeycomb-opentelemetry-javaagent:${distroVersion}"

    implementation("io.honeycomb:honeycomb-opentelemetry-sdk:${distroVersion}")
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

task copyAgentJar(type: Copy) {
    from configurations.agent {
        include '**/honeycomb-opentelemetry-javaagent*.jar'
    }
    into "agent"
    rename { fileName -> "honey-javaagent.jar" }
}

compileJava.dependsOn copyAgentJar
bootRun.dependsOn copyAgentJar

bootRun.doFirst {
    jvmArgs("-javaagent:agent/honey-javaagent.jar")
}

clean.doFirst {
    delete "agent"
}

test {
    useJUnitPlatform()
}
