plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
}

group = 'io.honeycomb.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

ext {
    otelVersion = '1.21.0'
    otelAgentVersion = '1.21.0'
}

configurations {
    agent
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation("io.opentelemetry:opentelemetry-api:${otelVersion}")
    implementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:${otelVersion}")

    agent "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelAgentVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

task copyAgentJar(type: Copy) {
    from configurations.agent {
        include '**/opentelemetry-javaagent*.jar'
    }
    into "agent"
    rename { fileName -> "otel-javaagent.jar" }
}

compileJava.dependsOn copyAgentJar
bootRun.dependsOn copyAgentJar

bootRun.doFirst {
    jvmArgs("-javaagent:agent/otel-javaagent.jar")
}

clean.doFirst {
    delete "agent"
}

test {
    useJUnitPlatform()
}
