plugins {
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
}

group = 'io.honeycomb.examples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
    otelAlphaVersion = '1.32.0-alpha'
    otelVersion = '1.32.0'
    otelAgentVersion = '1.32.0'
}

configurations {
    agent
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // Log4j
    implementation("org.apache.logging.log4j:log4j-api:2.17.2")
    implementation("org.apache.logging.log4j:log4j-core:2.17.2")

    // OpenTelemetry SDK
    implementation("io.opentelemetry:opentelemetry-sdk")
    implementation("io.opentelemetry:opentelemetry-api:${otelVersion}")
    implementation("io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations:${otelVersion}")

    agent "io.opentelemetry.javaagent:opentelemetry-javaagent:${otelAgentVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    // OpenTelemetry log4j appender
    implementation("io.opentelemetry.instrumentation:opentelemetry-log4j-appender-2.17:${otelAlphaVersion}")
}

task copyAgentJar(type: Copy) {
    from configurations.agent {
        include '**/opentelemetry-javaagent*.jar'
    }
    into "agent"
    rename { fileName -> "otel-javaagent.jar" }
}

compileJava.dependsOn copyAgentJar
bootRun.dependsOn copyAgentJar

bootRun.doFirst {
    jvmArgs("-javaagent:agent/otel-javaagent.jar")
}

clean.doFirst {
    delete "agent"
}

test {
    useJUnitPlatform()
}
